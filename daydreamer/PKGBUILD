# Maintainer: 3ED <krzysztof1987@gmail.com>
pkgname=daydreamer
pkgver=7.0
_fileid=0Byn3Yo_yFz45Skx6Vms0M3FBQ0k
pkgrel=1
pkgdesc="Day Dreamer is interactive fiction player written in java (ddz format)"
arch=('any')
url="https://drive.google.com/folderview?id=0Byn3Yo_yFz45ak11MXl6U05mYVE&usp=sharing"
license=('CCPL:BY-ND-NC')
depends=('java-common')
makedepends=('imagemagick')
install=daydreamer.install

source=("${pkgname}-${pkgver}.zip::https://drive.google.com/uc?export=download&id=$_fileid"
        "daydreamer-zip.mime"
        "daydreamer.sh"
        "daydreamer.desktop"
        "daydreamer.png")
sha256sums=('2325c682b68b189aa2de3eca00f135d1507659db1a14cd7f21ad1c5cec961bf3'
            '6a251e4ac8dcfd16bf7e77b9ba3503bdf355ff41d8d93ec43a644aff8e13171f'
            'f5d458aacae0022b5a55b4f02d2a6c42e6a2703a3d733795eed82144c9eb12dc'
            '20de8b29cadcb1431f12298c7275e33e7f66db6949f5b91d0ac4e8af766c47e8'
            '1770ada958ddaedd87bfe5942ae2fd3e4b6c2fb49f47fdbfeb0c430e81fc5434')

prepare() {
  cd "$srcdir"

  install -dm755 "$srcdir"/hicolor/256x256/apps/

  # Picture under CC, share and modification allowed, user/author: m-erencelik
  install -m644 "$srcdir/daydreamer.png" "$srcdir/hicolor/256x256/apps/daydreamer.png"

  for i in 16x16 24x24 32x32 48x48 64x64 96x96 128x128; do
    install -dm755 "$srcdir/hicolor/$i/apps/"
    convert -resize $i "$srcdir/daydreamer.png" "$srcdir/hicolor/$i/apps/daydreamer.png"
  done
}
package() {
  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgname,share/{doc/$pkgname,applications,icons,mime/packages}}
  install -m644 "$srcdir/DayDreamer_readme.txt" "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 "$srcdir/DayDreamer7.0.jar" "$pkgdir/usr/lib/$pkgname/"
  install -m755 "$srcdir/${pkgname}.sh" "$pkgdir/usr/bin/$pkgname"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/${pkgname}-zip.mime" "$pkgdir/usr/share/mime/packages/$pkgname-zip.xml"

  bsdtar -cf - hicolor | bsdtar -xf - -C "$pkgdir/usr/share/icons/"
}
