# Maintainer: 3ED <krzysztof1987@gmail.com>
pkgbase=daydreamer
pkgname=(daydreamer daydreamer-common daydreamer-editor)
pkgver=7.0
_app_player=("0Byn3Yo_yFz45Skx6Vms0M3FBQ0k" "7.0" "DayDreamer7.0.jar")
_app_editor=("0Byn3Yo_yFz45UFlrUlRyQjF2OEk" "7.0" "DayDreamerEditor7.0.jar")
pkgrel=3
pkgdesc="Day Dreamer is interactive fiction player written in java (ddg/ddz format)"
arch=('any')
url="https://drive.google.com/folderview?id=0Byn3Yo_yFz45ak11MXl6U05mYVE&usp=sharing"
license=('CCPL:BY-ND-NC')
depends=('java-common')
makedepends=('imagemagick')
install=daydreamer.install

source=("${pkgbase}-${_app_player[1]}.zip::https://drive.google.com/uc?export=download&id=${_app_player[0]}"
        "${pkgbase}-editor-${_app_editor[1]}.zip::https://drive.google.com/uc?export=download&id=${_app_editor[0]}"
        "daydreamer.mime"
        "launcher.sh"
        "daydreamer.desktop"
        "daydreamer-editor.desktop"
        "daydreamer.png")
sha256sums=('2325c682b68b189aa2de3eca00f135d1507659db1a14cd7f21ad1c5cec961bf3'
            '7f97fd8d27959446aa5d9e4e5237c3e718fc1fd1160eab812ce35c0ecd4cdb7a'
            '13b127124dfe88297e948b72b0be5d31c2dbe1620836cd3e36f260a3b98b7888'
            '416212b4990f2e78a45fb052e981226e80b34ba6ff968505c7c6dda48d6ac3a8'
            '3d8d1841b43eb9eea25aa5b16bdb3cbf2555d6616201c21344070989682727cd'
            'b12a2d0d5d3b8bf531135532b61058b03bb9bb1101c7987f27e6c45e25f3f3e9'
            '1770ada958ddaedd87bfe5942ae2fd3e4b6c2fb49f47fdbfeb0c430e81fc5434')

prepare() {
  cd "$srcdir"

  msg2 "Creating icons..."
  install -dm755 "$srcdir"/hicolor/256x256/apps/

  # Picture under CC, share and modification allowed, user/author: m-erencelik
  install -m644 "$srcdir/daydreamer.png" "$srcdir/hicolor/256x256/apps/daydreamer.png"

  for i in 16x16 24x24 32x32 48x48 64x64 96x96 128x128; do
    install -dm755 "$srcdir/hicolor/$i/apps/"
    convert -resize $i "$srcdir/daydreamer.png" "$srcdir/hicolor/$i/apps/daydreamer.png"
  done

  msg2 "Remove backup files from documentation..."
  find DayDreamerEditorHelp -name "*~" -delete

  msg2 "Creating launchers..."
  cat launcher.sh | tee daydreamer > daydreamer-editor
  echo 'launch_app "'"${_app_player[2]}"'"' >> daydreamer
  echo 'launch_app "'"${_app_editor[2]}"'"' >> daydreamer-editor

}

package_daydreamer-common() {
  pkgdesc="Commoin files for Day Dreamer player and editor"
  pkgver=1
  pkgrel=1
  install=daydreamer-common.install

  install -dm755 "$pkgdir"/usr/share/{icons,mime/packages}
  install -m644 "$srcdir/${pkgbase}.mime" "$pkgdir/usr/share/mime/packages/${pkgbase}.xml"
  bsdtar -cf - hicolor | bsdtar -xf - -C "$pkgdir/usr/share/icons/"
}

package_daydreamer() {
  depends+=('daydreamer-common')
  pkgver="${_app_player[1]}"
  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgbase,share/{doc/$pkgname,applications}}
  install -m644 "$srcdir/DayDreamer_readme.txt" "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 "$srcdir/${_app_player[2]}" "$pkgdir/usr/lib/$pkgbase/"
  install -m755 "$srcdir/${pkgname}" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
}

package_daydreamer-editor() {
  depends+=('daydreamer-common')
  pkgdesc="Editor for Day Dreamer interactive fiction games"
  pkgver="${_app_editor[1]}"

  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgbase,share/{doc/$pkgname,applications}}
  install -m644 "$srcdir/${_app_editor[2]}" "$pkgdir/usr/lib/$pkgbase/"
  install -m755 "$srcdir/${pkgname}" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
  install -m644 DayDreamerEditorHelp.html "$pkgdir/usr/share/doc/$pkgname/"
  bsdtar -cf - DayDreamerEditorHelp | bsdtar -xf - -C "$pkgdir/usr/share/doc/$pkgname/"
}
