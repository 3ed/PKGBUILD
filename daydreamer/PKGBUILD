# Maintainer: 3ED <krzysztof1987@gmail.com>
pkgbase=daydreamer
pkgname=(daydreamer daydreamer-common daydreamer-editor)
pkgver=7.0
pkgrel=4
_app_player=("0Byn3Yo_yFz45Skx6Vms0M3FBQ0k" "7.0" "DayDreamer7.0.jar")
_app_editor=("0Byn3Yo_yFz45UFlrUlRyQjF2OEk" "7.0" "DayDreamerEditor7.0.jar")
pkgdesc="Day Dreamer is interactive fiction player written in java (ddg/ddz format)"
arch=('any')
url="https://drive.google.com/folderview?id=0Byn3Yo_yFz45ak11MXl6U05mYVE&usp=sharing"
license=('CCPL:BY-ND-NC')
depends=('java-runtime-common')
makedepends=('python2-pillow')
install=daydreamer.install

source=("${pkgbase}-${_app_player[1]}.zip::https://drive.google.com/uc?export=download&id=${_app_player[0]}"
        "${pkgbase}-editor-${_app_editor[1]}.zip::https://drive.google.com/uc?export=download&id=${_app_editor[0]}"
        "daydreamer.mime"
        "launcher.sh"
        "daydreamer.desktop"
        "daydreamer-editor.desktop"
        "daydreamer.png")
sha512sums=('6581db60b4b9368fe5be44d49cf25aef482759fe13a3476e5d3408bdb5248779f24f9c1d4fd3c497e43173068edf366b05434e1ddb4d278c118b427100a78cd3'
            '01fca73578951f68a77a44ca7218706e00e15a37624c93fe1e998abfd4ebf945bf5caaa9ef5f65b8ec74ac25b9ad453078c77cd31006feb21605a7a8cae312ba'
            '920efe2525ce815a4668a225d3612a622fe73f12ff69cda8913f80b46c5fd60b494034e4ab9593a7bf31f9be82f7dd2510388de843877f22c8d91284285054e1'
            'b02deb777a20e5adba60829a4916b3f13f22a9334d06068d4c73cd72a3863201eba2d6917171479f293cfe87b608538386a335a277876dc47c73604d09598759'
            'c812d292be1c455d006f8909527718a9d93dd9bb071e8cf66cdafea2f6860fb90a12a0776f677e1c09c2c5d3912e42397c573ef2c74fab1c608893ec1ad6842c'
            'f1e5e63fc4cf633a8a51d5d816e2098ec114d60fa512aa5e6cd2311d133415b8ffda4a1956afdd47cff04ab01ee266e7ce3109138390cfe9befd5ccad07bc661'
            'e9ff634abeeeee32fc1a8e25f2b3c16148b8926ee833b12c520a5a8ea1ae5b7bbecf3399be6772c6502051f6f26eed1c8f398234157a8349ffeff45d54f9b168')

prepare() {
  cd "$srcdir"

  msg2 "Creating icons..."
  install -dm755 "$srcdir"/hicolor/{16x16,24x24,32x32,48x48,64x64,96x96,128x128,256x256}/apps/

  # Picture under CC, share and modification allowed, user/author: m-erencelik @deviantart
  install -m644 "$srcdir/daydreamer.png" "$srcdir/hicolor/256x256/apps/daydreamer.png"

  python2 <<EOF
from __future__ import print_function
from sys import stderr
from PIL import Image

try:
    orig = Image.open("daydreamer.png")
except IOError:
    print("cannot open: daydreamer.png", file=stderr)
    exit(1)

for resx in (16, 24, 32, 48, 64, 96, 128):
    img_fname = "%s%d%s%d%s" % ("hicolor/", resx, "x", resx, "/apps/daydreamer.png")
    img = orig.resize((resx, resx))

    try:
        img.save(img_fname)
    except IOError:
        print("cannot write:", img_fname, file=stderr)
        exit(1)
EOF

  msg2 "Remove backup files from documentation..."
  find DayDreamerEditorHelp -name "*~" -delete

  msg2 "Creating launchers..."
  cat launcher.sh | tee daydreamer > daydreamer-editor
  echo 'launch_app "'"${_app_player[2]}"'"' >> daydreamer
  echo 'launch_app "'"${_app_editor[2]}"'"' >> daydreamer-editor

}

package_daydreamer-common() {
  pkgdesc="Commoin files for Day Dreamer player and editor"
  install=daydreamer-common.install

  install -dm755 "$pkgdir"/usr/share/{icons,mime/packages}
  install -m644 "$srcdir/${pkgbase}.mime" "$pkgdir/usr/share/mime/packages/${pkgbase}.xml"
  bsdtar -cf - hicolor | bsdtar -xf - -C "$pkgdir/usr/share/icons/"
}

package_daydreamer() {
  depends+=('daydreamer-common')
  pkgver="${_app_player[1]}"
  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgbase,share/{doc/$pkgname,applications}}
  install -m644 "$srcdir/DayDreamer_readme.txt" "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 "$srcdir/${_app_player[2]}" "$pkgdir/usr/lib/$pkgbase/"
  install -m755 "$srcdir/${pkgname}" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
}

package_daydreamer-editor() {
  depends+=('daydreamer-common')
  pkgdesc="Editor for Day Dreamer interactive fiction games"
  pkgver="${_app_editor[1]}"

  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgbase,share/{doc/$pkgname,applications}}
  install -m644 "$srcdir/${_app_editor[2]}" "$pkgdir/usr/lib/$pkgbase/"
  install -m755 "$srcdir/${pkgname}" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
  install -m644 DayDreamerEditorHelp.html "$pkgdir/usr/share/doc/$pkgname/"
  bsdtar -cf - DayDreamerEditorHelp | bsdtar -xf - -C "$pkgdir/usr/share/doc/$pkgname/"
}
