# Maintainer: 3ED <krzysztof1987@gmail.com>
pkgbase=daydreamer
pkgname=(daydreamer daydreamer-common daydreamer-editor)
pkgver=8.1
pkgrel=1
_app_player=("0Byn3Yo_yFz45ZWFJdVd0OFNPMG8" "8.1" "DayDreamer8.1.jar")
_app_editor=("0Byn3Yo_yFz45VUNwQlBQNkcyX1k" "8.1" "DayDreamerEditor8.1.jar")
pkgdesc="Day Dreamer is interactive fiction player written in java (ddg/ddz format)"
arch=('any')
url="https://drive.google.com/folderview?id=0Byn3Yo_yFz45ak11MXl6U05mYVE&usp=sharing"
license=('CCPL:BY-ND-NC')
depends=('java-runtime-common')
install=daydreamer.install

source=("${pkgbase}-${_app_player[1]}.zip::https://drive.google.com/uc?export=download&id=${_app_player[0]}"
        "${pkgbase}-editor-${_app_editor[1]}.zip::https://drive.google.com/uc?export=download&id=${_app_editor[0]}"
        "daydreamer.mime"
        "launcher.sh"
        "daydreamer.desktop"
        "daydreamer-editor.desktop"
        "daydreamer-save.svg"
        "daydreamer-game.svg"
        "daydreamer.svg")
sha512sums=('4cfeffb34cc67430fc16c56760c6ad62ec44b274d1e11075cc813044cf2819fddc15251ec07bcff58d9449c8aac387cd1db8431d7fcba7c138858c9d925ba9a7'
            '9d3ebae1ba26051f4234fd7887dd202edf076eee9628152e3d1c03df7816239a1ccf756f337efbc13f35db35286d3dbb2a552ade353a182dee53b16476c03307'
            'a02799b7337166bc33645d6e4beaecdfac7e622ea5d5e876370a512fb261577bf619cb68b1776b95cb0014a4ee90ea84efaa26a08124087363fa3cebb52427a8'
            'b02deb777a20e5adba60829a4916b3f13f22a9334d06068d4c73cd72a3863201eba2d6917171479f293cfe87b608538386a335a277876dc47c73604d09598759'
            'c812d292be1c455d006f8909527718a9d93dd9bb071e8cf66cdafea2f6860fb90a12a0776f677e1c09c2c5d3912e42397c573ef2c74fab1c608893ec1ad6842c'
            'f1e5e63fc4cf633a8a51d5d816e2098ec114d60fa512aa5e6cd2311d133415b8ffda4a1956afdd47cff04ab01ee266e7ce3109138390cfe9befd5ccad07bc661'
            '7f99b9baaa1539cdc6ca1cac70d739b34965b0581e4761162f20e93e27ebc799125f94d2611b3b168a38caddb7ed75ab613e898abdb7d414d814115076d2dde5'
            '716d856e6ca132fd3d7d5cb1a8a72a7ab749f9d55061f665d7be98a5b6a6a932fe2dd99875cdc71bc719bcbf5c94029d3f3e4ae1fa822d29df5627914121286d'
            '9b61145dd74f2a976d757d3926f752d1b861e0ebbfbe1c7a885c936274f8a6598913c637c752618f06bf64c9586b503d12bf69ccf41fd5435dfb9e7b7b361fef')
prepare() {
  cd "$srcdir"

  msg2 "Creating icons..."
  install -dm755 "$srcdir/hicolor/scalable/"{apps,mimetypes}/

  # Picture under CC, share and modification allowed, user/author: m-erencelik @deviantart
  # // vectorized by 3ED (inkscape)
  install -m644 "$srcdir/daydreamer.svg" "$srcdir/hicolor/scalable/apps/daydreamer.svg"

  for i in 16x16 24x24 32x32 48x48 64x64 96x96 128x128 256x256; do
    install -dm755 "$srcdir/hicolor/$i/apps/"
    rsvg-convert -w "${i%%x*}" -h "${i##*x}" -f png \
      -o "$srcdir/hicolor/$i/apps/daydreamer.png" "$srcdir/daydreamer.svg"
  done

  for i in daydreamer-game daydreamer-save; do
    # Picture under CC, share and modification allowed, user/author: m-erencelik @deviantart
    # // vectorized by 3ED (inkscape)
    install -m644 "$srcdir/$i.svg" "$srcdir/hicolor/scalable/mimetypes/application-x-$i.svg"

    for ii in 16x16 24x24 32x32 48x48 64x64 96x96 128x128 256x256; do
      install -dm755 "$srcdir/hicolor/$ii/mimetypes/"
      rsvg-convert -w "${ii%%x*}" -h "${ii##*x}" -f png \
        -o "$srcdir/hicolor/$ii/mimetypes/application-x-$i.png" "$srcdir/$i.svg"
    done
  done

  msg2 "Remove backup files from documentation..."
  find DayDreamerEditorHelp -name "*~" -delete

  msg2 "Creating launchers..."
  cat launcher.sh | tee daydreamer > daydreamer-editor
  echo 'launch_app "'"${_app_player[2]}"'"' >> daydreamer
  echo 'launch_app "'"${_app_editor[2]}"'"' >> daydreamer-editor

}

package_daydreamer-common() {
  pkgdesc="Commoin files for Day Dreamer player and editor"
  install=daydreamer-common.install

  install -dm755 "$pkgdir"/usr/share/{icons,mime/packages}
  install -m644 "$srcdir/${pkgbase}.mime" "$pkgdir/usr/share/mime/packages/${pkgbase}.xml"
  bsdtar -cf - hicolor | bsdtar -xf - -C "$pkgdir/usr/share/icons/"
}

package_daydreamer() {
  depends+=('daydreamer-common')
  pkgver="${_app_player[1]}"
  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgbase,share/{doc/$pkgname,applications}}
  install -m644 "$srcdir/DayDreamer_readme.txt" "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 "$srcdir/${_app_player[2]}" "$pkgdir/usr/lib/$pkgbase/"
  install -m755 "$srcdir/${pkgname}" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
}

package_daydreamer-editor() {
  depends+=('daydreamer-common')
  pkgdesc="Editor for Day Dreamer interactive fiction games"
  pkgver="${_app_editor[1]}"

  install -dm755 "$pkgdir"/usr/{bin,lib/$pkgbase,share/{doc/$pkgname,applications}}
  install -m644 "$srcdir/${_app_editor[2]}" "$pkgdir/usr/lib/$pkgbase/"
  install -m755 "$srcdir/${pkgname}" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/"
  install -m644 DayDreamerEditorHelp.html "$pkgdir/usr/share/doc/$pkgname/"
  bsdtar -cf - DayDreamerEditorHelp | bsdtar -xf - -C "$pkgdir/usr/share/doc/$pkgname/"
}
