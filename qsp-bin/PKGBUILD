# Maintainer: 3ED <krzysztof1987@gmail.com>
pkgname=qsp-bin
pkgver=5.7.0
pkgrel=1
pkgdesc="Quest Soft Player is another interactive story player (it's own qsp format)"
arch=('i686' 'x86_64')
url="http://qsp.su/"
license=('GPL' 'LGPL')
depends=('libjpeg6' 'libpng12' 'gtk2' 'cairo' 'libsm')
install=qsp-gui.install

_tarname="qsp${pkgver//\./}"

[ "$CARCH" = "i686" ] || _tarname+=_64

source=("http://qsp.su/attachments/${_tarname}.tar.bz2"
        "https://raw.githubusercontent.com/QSPFoundation/qsp/45c4e7180704b2940e547eb041bbc5b57c46d9e2/help/qsp-logo-vector.svg"
        "qsp-qui.sh"
        "qsp-gui.desktop")
sha256sums=('fabd48411d59d18b9c663a96490316634c5ef9bdf4208f559c952f9581299dda'
            '12de4d6a91bdc09d334585b04dbb3d2e9ae7277f0274090b9f73c812d414e5dc'
            'f9fee23b9f5da3d6423706dd631bed8e5b3f6bd6588ae1f314d843a4d1c38438'
            'ebd39d2c88fd88f7c381fad74804f5c5c0f92d14ff738ca8498250c88b8d6b64')

[ "$CARCH" = "i686" ] || sha256sums[0]=('0d1f22209fd0ab39c64b8834634f599de16ac96cb03b30ab385ce57a5eb6022f')

prepare() {
  cd "$srcdir"

  install -dm755 "$srcdir/hicolor/scalable/apps/"
  install -m644 "$srcdir/qsp-logo-vector.svg" "$srcdir/hicolor/scalable/apps/qsp-gui.svg"

  for i in 16x16 24x24 32x32 48x48 64x64 96x96 128x128 256x256; do
    install -dm755 "$srcdir/hicolor/$i/apps/"
    rsvg-convert -w "${i%%x*}" -h "${i##*x}" -f png \
      -o "$srcdir/hicolor/$i/apps/qsp-gui.png" "$srcdir/qsp-logo-vector.svg"
  done
}
package() {
  install -dm755 "$pkgdir"/usr/{bin,lib/qsp,share/{applications,icons}}
  install -m755 "$srcdir/qsp-qui.sh" "$pkgdir/usr/bin/qsp-gui"
  install -m644 "$srcdir/qsp-gui.desktop" "$pkgdir/usr/share/applications/"

  cd "$srcdir"
	bsdtar -cf - hicolor | bsdtar -xf - -C "$pkgdir/usr/share/icons/"

  cd "$_tarname"
	bsdtar -cf - * | bsdtar -xf - -C "$pkgdir/usr/lib/qsp/"
}
