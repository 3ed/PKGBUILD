#Mantainer: 3ED_0 <krzysztof1987 at gmail>

pkgname=gtk-theme-zukiwi-bzr
_pkgname=zukitwo
pkgver=v3.12.r53
_pkgver=3.12
pkgrel=1
pkgdesc="A theme for GTK3, GTK2, Metacity, xfwm4 and Unity."
arch=('any')
url="http://gnome-look.org/content/show.php/Zukitwo?content=140562"
license=('GPL3')
makedepends=('bzr')
depends=('gtk-engine-murrine' 'gtk-engines')
optdepends=('gnome-themes-standard: Required for the GTK3 theme'
            'ttf-droid: Font family for the Gnome Shell theme')
conflicts=('gtk-theme-zukiwi')
provides=('gtk-theme-zukiwi')
source=("${_pkgname}-${_pkgver}::bzr+http://bazaar.launchpad.net/~lassekongo83/${_pkgname}/gnome${_pkgver}/")
sha1sums=('SKIP')

pkgver() {
  cd "${_pkgname}-${_pkgver}"
  printf "v%s.r%s" "${_pkgver}" "$(bzr revno)"
}

package() {
  cd "${_pkgname}-${_pkgver}"
  install -dm755 "$pkgdir/usr/share/themes/"
  bsdtar -cf - Zukiwi*/ | bsdtar -xf - -C "$pkgdir/usr/share/themes/"
  chmod -R a=r,a+X,u+w "$pkgdir/usr/share/themes/"
}

# vim:set ts=2 sw=2 et:
